allprojects {
    apply plugin: 'jacoco'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'maven'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    group = 'org.cadixdev'
    archivesBaseName = project.name.toLowerCase()
    version = '0.4.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile 'org.codehaus.groovy:groovy-all:2.5.8'
        testCompile 'org.spockframework:spock-core:1.3-groovy-2.5'
        testCompile 'org.junit.jupiter:junit-jupiter-api:5.5.1'
        testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.5.1'
        testRuntime 'org.junit.vintage:junit-vintage-engine:5.5.1'
    }

    test {
        useJUnitPlatform()
    }

    processResources {
        from 'LICENSE.txt'
    }

    javadoc {
        options.encoding = 'UTF-8'
        options.charSet = 'UTF-8'
        options.links(
                'https://docs.oracle.com/javase/8/docs/api/'
        )
    }

    task javadocJar(type: Jar, dependsOn: 'javadoc') {
        from javadoc.destinationDir
        classifier = 'javadoc'
    }

    task sourcesJar(type: Jar, dependsOn: 'classes') {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    artifacts {
        archives javadocJar
        archives sourcesJar
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }
    check.dependsOn jacocoTestReport

    if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
        apply plugin: 'signing'
        signing {
            required { !version.endsWith('-SNAPSHOT') && gradle.taskGraph.hasTask(tasks.uploadArchives) }
            sign configurations.archives
        }
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                // Maven Central
                if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                    repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }
                }

                pom {
                    artifactId = project.archivesBaseName

                    project {
                        name = project.name
                        description = project.description
                        packaging = 'jar'
                        url = project.url
                        inceptionYear = project.inceptionYear

                        scm {
                            url = 'https://github.com/CadixDev/Bombe'
                            connection = 'scm:git:https://github.com/CadixDev/Bombe.git'
                            developerConnection = 'scm:git:git@github.com:CadixDev/Bombe.git'
                        }

                        issueManagement {
                            system = 'GitHub'
                            url = 'https://github.com/CadixDev/Bombe/issues'
                        }

                        licenses {
                            license {
                                name = 'BSD 3-Clause'
                                url = 'https://opensource.org/licenses/BSD-3-Clause'
                                distribution = 'repo'
                            }
                        }

                        developers {
                            developer {
                                id = 'jamierocks'
                                name = 'Jamie Mansfield'
                                email = 'dev@jamierocks.uk'
                                url = 'https://www.jamiemansfield.me/'
                                timezone = 'Europe/London'
                            }
                        }
                    }
                }
            }
        }
    }
}

task codeCoverageReport(type: JacocoReport) {
    // Gather execution data from all subprojects
    // (change this if you e.g. want to calculate unit test/integration test coverage separately)
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    // Add all relevant sourcesets from the subprojects
    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled = true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled = true
        html.destination file("${buildDir}/reports/jacoco/html")
    }
}

// always run the tests before generating the report
codeCoverageReport.dependsOn {
    subprojects*.test
}
